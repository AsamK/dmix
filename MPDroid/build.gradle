buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

def gitShortHash() {
    return "git rev-parse --short HEAD".execute().text.replace("\n", "")
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 48
        versionName "1.07 Beta 8 " + gitShortHash()
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        closedbits {
            versionName defaultConfig.versionName + "-c"
        }

        foss {
            versionName defaultConfig.versionName + "-f"
            ext.enableCrashlytics = false
        }
    }
}

dependencies {

    // Support Libraries
    compile 'com.android.support:support-v4:20.+'

    // Projects
    compile project(':JMPDCommAndroid')

    // Libraries from maven
    compile 'com.sothree.slidinguppanel:library:2.0.1'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.3'
    compile 'asia.ivity.android:drag-sort-listview:1.0'
    closedbitsCompile 'com.crashlytics.android:crashlytics:1.+'
}